buildscript {
    ext.isPlay = false
    ext.buildConfig = [
            'compileSdk': 30,
            'minSdk'    : 21,
            'targetSdk' : 30,
            'buildTools': '30.0.3',
            'version'   : [
                    'major': 2,
                    'minor': 8,
                    'patch': 2,
                    'build': 0,
                    'code' : 68
            ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"

    repositories {
        mavenCentral()
        google()
        mavenLocal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-beta03'
        classpath(libs.gradle.plugin.sqldelight)
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.10"
        classpath(libs.gradle.plugin.hilt)
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.15'
        if (isPlay) {
            classpath 'com.google.gms:google-services:4.3.8'
            classpath 'com.google.firebase:firebase-crashlytics-gradle:2.6.1'
        }
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://androidx.dev/snapshots/builds/7437341/artifacts/repository' }
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }
    }

    tasks.withType(JavaCompile).configureEach { task ->
        task.sourceCompatibility = JavaVersion.VERSION_11
        task.targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
        task.kotlinOptions {
            jvmTarget = JavaVersion.VERSION_11
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinCompile).configureEach { task ->
        task.kotlinOptions {
            useIR = true
            freeCompilerArgs += [
                    '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
                    '-Xopt-in=kotlinx.coroutines.ObsoleteCoroutinesApi',
                    '-Xopt-in=kotlin.time.ExperimentalTime',
                    '-Xopt-in=kotlinx.coroutines.FlowPreview',
                    '-Xopt-in=kotlin.ExperimentalStdlibApi',
                    '-Xopt-in=androidx.compose.animation.ExperimentalAnimationApi',
                    '-Xopt-in=androidx.compose.material.ExperimentalMaterialApi'
            ]
        }
    }
}



